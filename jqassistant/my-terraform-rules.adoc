== Project Specific Concepts & Constraints

[[my-terraform-rules:LBs-must-use-free-EIPs-only]]
.A Load Balancer needs free Elastic IPs - they must not be bound to a Network Interface
[source,cypher,role=constraint,severity=major]
----
MATCH (aws_lbs:HCL {name:'aws_lb'})
        -[*]->()
        -[:REFERS_TO]-> (id {identifier: 'id'})
        <-[:HAS_ATTRIBUTE]- (eip:HCL)
        <-[:HAS_BLOCK]- (aws_eips:HCL {name: 'aws_eip'})
WITH eip
MATCH (file:HCL:File)
        -[*]-> (eip)
        -[:HAS_ATTRIBUTE]-> (network_interface:HCL {identifier: 'network_interface'})
WHERE NOT (file:Configuration)
RETURN DISTINCT file, eip
----

[[my-terraform-rules:terraform-resources-report]]
.Terraform Resources
[source,cypher,role=concept,reportType="Groovy",reportProperties="scriptname=jqassistant/groovy/tst.groovy"]
----
MATCH (hclFile:HCL:File )
    --> (resourceBlock:HCL:Block {name: 'resource'})
    --> (resourceType:HCL:Block)
    --> (resourceName:HCL:Block)
RETURN hclFile.fileName as file,
    resourceType.name as type,
    resourceName.name as name
----
