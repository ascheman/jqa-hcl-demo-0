== Project Specific Concepts & Constraints

[[my-terraform-rules:LBs-must-use-free-EIPs-only]]
.A Load Balancer needs free Elastic IPs - they must not be bound to a Network Interface
[source,cypher,role=constraint,severity=major,requiresConcepts="terraform:ResourceUsageRelations"]
----
MATCH (aws_lbs:Terraform:Resource {type:'aws_lb'})
        -[:USES]->(aws_eip:HCL {type: 'aws_eip'})
WITH aws_eip
MATCH (eip_file:Terraform:File)
        -[*]-> (aws_eip)
        -[:USES]-> (aws_ni:Terraform:Resource {type: 'aws_network_interface'})
        <-[*]-(ni_file:Terraform:File)
WHERE NOT (eip_file:Configuration) AND NOT (ni_file:Configuration)
RETURN DISTINCT eip_file, aws_eip, ni_file, aws_ni
----

[[my-terraform-rules:terraform-resources-report]]
.Terraform Resources
[source,cypher,role=concept,reportType="Groovy",reportProperties="scriptname=jqassistant/groovy/terraformResourcesReport.groovy",requiresConcepts="terraform:File2Resource"]
----
MATCH (file:Terraform:File )
    -[:DEFINES]-> (resource:Terraform:Resource)
RETURN DISTINCT file.fileName AS file,
    resource.type AS type,
    resource.name AS name
ORDER BY file, type
----

[[my-terraform-rules:usage-report]]
.Terraform Resource Relationships
[source,cypher,role=concept,reportType="Groovy",reportProperties="scriptname=jqassistant/groovy/terraformUsage.groovy",requiresConcepts="terraform:ResourceUsageRelations"]
----
MATCH (src:Terraform:Resource )
    -[:USES]-> (tgt:Terraform:Resource)
SET src.displayname = src.type + "." + src.name
SET tgt.displayname = tgt.type + "." + tgt.name
RETURN src.displayname AS src, tgt.displayname AS tgt
----
